buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://palantir.bintray.com/releases'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:1.9.2'
        classpath 'com.palantir.baseline:gradle-baseline-java:0.61.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:0.11.0'
        classpath 'gradle.plugin.org.inferred:gradle-processors:2.2.0'
    }
}

apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.consistent-versions'

allprojects {
    version gitVersion()
    group 'com.palantir.eventsourcingexperiments'

    repositories {
        jcenter()
        maven {
            url 'https://palantir.bintray.com/releases'
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'org.inferred.processors'
    apply plugin: 'com.palantir.baseline-class-uniqueness'

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Werror']
    }

    tasks.check.dependsOn(javadoc)
    sourceCompatibility = 1.8
}
